Sub OCRSpellingHandler()

On Error GoTo ErrorHandler

Dim MsgBoxResult As VbMsgBoxResult
Dim a As Variant, b As Variant
Dim i As Integer

' ダイアログによる処理の選択
MsgBoxResult = MsgBox( _
    "実行する処理を選択してください。（※文書全体を処理します）" & vbCrLf & _
    vbCrLf & _
    "(1) はい（Yes）→改行の修正" & vbCrLf & _
    "(2) いいえ（No）→文字の修正（OCRが誤認識しやすい（？）文字を一括置換）" & vbCrLf & _
    "(3) キャンセル（Cancel）→何もせず終了", _
    vbYesNoCancel, _
    "処理の選択" _
    )

' 選択に応じた処理の分岐
If MsgBoxResult = vbCancel Then
    MsgBox "何もせず終了します。"
    Exit Sub
ElseIf MsgBoxResult = vbYes Then
    ' aは検索する文字列、bは置換後の文字列（aの1番目の文字列は、bの1番目の文字列に置換、aの2番めはbの2番目、以下Forループ）
    ' ParagraphMark→削除。ただし(1)直前に半角スペースのあるParagraphMark→改めてParagraphMark、(2)直前に。．のあるParagraphMarkと二連続ParagraphMark→残すように一括置換
    a = Array(" ^p", "。^p", "｡^p", "．^p", ".^p", "」^p", "｣^p", "^p^p", "^p", _
              " 「半角スペースP用目印」 ", " 「。P用目印」 ", " 「｡P用目印」 ", " 「．P用目印」 ", " 「.P用目印」 ", " 『」P用目印』 ", " 『｣P用目印』 ", " 「二連続P用目印」 ")
    b = Array(" 「半角スペースP用目印」 ", " 「。P用目印」 ", " 「｡P用目印」 ", " 「．P用目印」 ", " 「.P用目印」 ", " 『」P用目印』 ", " 『｣P用目印』 ", " 「二連続P用目印」 ", "", _
              "^p", "。^p", "｡^p", "．^p", ".^p", "」^p", "｣^p", "^p^p")
ElseIf MsgBoxResult = vbNo Then
    ' aは検索する文字列、bは置換後の文字列（aの1番目の文字列は、bの1番目の文字列に置換、aの2番めはbの2番目、以下Forループ）
    a = Array("(", ")", "､", "｡", "．", "･", " （", "） ", "、 ", "。 ", ". ", "l", "｢", "｣" _
              , "x", "X'", "XI", "Xl", "X】", "y", "Y'", "YI", "Yl", "Y】", "z", "X.Y", "X.Z", "Y.Z" _
              , "|司", "I司", "償権", "求債権", "訴求償権", "本間", "異識", "職成", "賠悩", "諦求", "誠求", "俄権" _
              , "憤権", "偵権", "償務", "憤務", "備務", "偵務", ":", "1Q", "l", "Lz", "'" _
              , "当郡者", "袖足", "椛", "保謹", "過言", "遡言", "柵", "椎", "櫛成", "記栽" _
              )
    b = Array("（", "）", "、", "。", ".", ".", "（", "）", "、", "。", ".", "1", "「", "」" _
              , "X", "X1", "X1", "X1", "X1", "Y", "Y1", "Y1", "Y1", "Y1", "Z", "X・Y", "X・Z", "Y・Z" _
              , "同", "同", "債権", "求償権", "訴求債権", "本問", "異議", "構成", "賠償", "請求", "請求", "債権" _
              , "債権", "債権", "債務", "債務", "債務", "債務", "：", "LQ", "1", "L2", "1" _
              , "当事者", "補足", "権", "保護", "遺言", "遺言", "欄", "権", "構成", "記載" _
              )
End If

' 確認ダイアログ停止（自動でYes）
Application.DisplayAlerts = False

' 置換
For i = LBound(a) To UBound(a)
    With selection.Find
        .text = a(i)
        .Replacement.text = b(i)
        .Forward = True
        .Wrap = wdFindAsk
        .Format = False
        .MatchCase = False
        .MatchWholeWord = True
        .MatchByte = True
        .MatchAllWordForms = False
        .MatchSoundsLike = False
        .MatchWildcards = False
        .MatchFuzzy = False
    End With
    selection.Find.Execute Replace:=wdReplaceAll
Next i

' 確認ダイアログ復活
Application.DisplayAlerts = True

' 完了通知メッセージ
MsgBox Join(a, " :: ") & vbCrLf & _
       vbCrLf & _
       "を" & vbCrLf & _
       vbCrLf & _
       Join(b, " :: ") & vbCrLf & _
       vbCrLf & _
       "に置き換えました。", vbInformation, "完了"

Exit Sub

ErrorHandler:
    MsgBox "エラーが発生しました。" & vbCrLf & _
           "エラー番号：" & Err.Number & vbCrLf & _
           "エラーの説明：" & Err.Description, _
           vbCritical, "エラーメッセージ"

End Sub
